buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/libs-milestone' }
	}

	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.0.M1'
        classpath 'org.flywaydb:flyway-gradle-plugin:4.2.0'
        classpath 'org.postgresql:postgresql:9.4.1212'
	}
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/libs-milestone" }
}

tasks.withType(JavaCompile) {
    // enable parameter name preservation, used by Jackson
    options.compilerArgs.add('-parameters')
}

bootJar {
    archiveName = 'globe42.jar'
    dependsOn ':frontend:npm_run_build'

    into('BOOT-INF/classes/static') {
        from "${project(':frontend').projectDir}/dist"
    }
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-data-jpa'
	compile 'org.springframework.boot:spring-boot-starter-web'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'com.ninja-squad:DbSetup:2.1.0'
	runtime 'org.postgresql:postgresql:9.4.1212'
	runtime 'org.flywaydb:flyway-core'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compile 'com.fasterxml.jackson.module:jackson-module-parameter-names'
    compile 'io.jsonwebtoken:jjwt:0.7.0'
}

task flywayCleanInteg(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    url = 'jdbc:postgresql://localhost:5432/globe42'
    user = 'globe42'
    password = 'globe42'
}

task flywayCleanTest(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    url = 'jdbc:postgresql://localhost:5432/globe42_test'
    user = 'globe42'
    password = 'globe42'
}
